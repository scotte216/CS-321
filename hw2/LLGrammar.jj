// This is supporting software for CS321/CS322 Compilers and Language Design.
// Copyright (c) Portland State University.
//---------------------------------------------------------------------------
// For CS321 F'15 (J. Li).
// Scott Ewing 
// 

// A raw grammar for the miniJava language. (JavaCC Specification)
//

// options { DEBUG_PARSER=true; }  /* Show debugging info */

PARSER_BEGIN(LLGrammar)
import java.io.*;

public class LLGrammar {
  public static void main(String [] args) {
    if (args.length < 1) {
      System.out.println("Need a file name as command-line argument.");
      return;
    } 
    try {
      FileReader input = new FileReader(args[0]);
      new LLGrammar(input).Program();
      input.close();
    } catch (TokenMgrError e) {
      System.err.println(e);
    } catch (Exception e) {
      System.err.println(e);
    }
  }
}
PARSER_END(LLGrammar)

//
// LEXER SECTION ---------------------------------------------------------------
//
// A simplified version. Error detections are not included.
// 
SKIP:  /* Whitespace */
{ 
  " " | "\t" | "\r" | "\n" 
}

SKIP:  /* Comments -- single-line form only */
{
  <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> 
}

TOKEN:  /* Keywords */
{
  "class" | "extends" | "static" | "public" | "main" | "void" | "boolean"
| "int" | "double" | "String" | "true" | "false" | "new" | "this" | "if"
| "else" | "while" | "return" | "System" | "out" | "println"
}

TOKEN:  /* Literals and IDs */
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: ["A"-"Z"]|["a"-"z"]>
| <INTLIT:  "0"|["1"-"9"](<DIGIT>)*> 	// decimal form only
| <DBLLIT:  (<DIGIT>)+"."(<DIGIT>)*|(<DIGIT>)*"."(<DIGIT>)+> 
| <STRLIT:  ("\"" (~["\"","\r","\n"])* "\"")>
| <ID:      <LETTER> (<LETTER>|<DIGIT>)*>
}

TOKEN:  /* Operators and delimiters */
{
  "+" | "-" | "*" | "/" | "&&" | "||" | "!" | "==" | "!=" | "<" | "<=" 
| ">" | ">=" |  "=" | ";" | "," | "." | "(" | ")" | "[" | "]" | "{" | "}"
}

//
// PARSER SECTION ---------------------------------------------------------------
//

// Program -> {ClassDecl}
//
void Program(): {}
{
  (ClassDecl())* <EOF>
}

// ClassDecl -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {"public" MethodDecl} "}"
//
void ClassDecl(): {}
{
  "class" <ID> ["extends" <ID>] "{" (VarDecl())* ("public" MethodDecl())* "}"
}

// MethodDecl -> ExtType <ID> "(" [Param {"," Param}] ")" 
//                 "{" {VarDecl} {Stmt} "}"
//            |  "static" "void" "main" "(" "String" "[" "]" <ID> ")"
//                 "{" {VarDecl} {Stmt} "}" 
// 
void MethodDecl(): {} 
{
   
   ExtType() <ID> "(" [Param() ("," Param())*] ")" 
          "{" (LOOKAHEAD(2) VarDecl())* (Stmt())* "}"
|   "static" "void" "main" "(" "String" "[" "]" <ID> ")"
          "{" ( LOOKAHEAD(2) VarDecl())* (Stmt())* "}"
}

// Param -> Type <ID> 
//
void Param(): {} 
{
  Type() <ID> 
}

// VarDecl -> Type <ID> ["=" Expr] ";"

//  
void VarDecl(): {}
{
  Type() <ID> ["=" Expr()] ";"
}

// ExtType -> Type | "void"
//
void ExtType(): {}
{
  Type() | "void"
}

// Type -> BasicType [ "[" "]" ]
//      |  <ID>
//
void Type(): {}
{
  BasicType() ["[" "]"]
| <ID>
}

// BasicType -> "int" | "double" | "boolean"
//
void BasicType(): {}
{
  "int" | "double" | "boolean"
}

// Stmt -> "{" {Stmt} "}"
//      |  ExtID ExprOrAssign 
//      |  "if" "(" Expr ")" Stmt ["else" Stmt]
//      |  "while" "(" Expr ")" Stmt
//      |  "System" "." "out" "." "println" "(" [PrArg] ")" ";"
//      |  "return" [Expr] ";"
//
void Stmt(): {}
{
  "{" (Stmt())* "}"
| ExtId() ExprOrAssign() 
| "if" "(" Expr() ")" Stmt() ["else" Stmt()]
| "while" "(" Expr() ")" Stmt()
| "System" "." "out" "." "println" "(" [ PrArg() ] ")" ";"  
| "return" [Expr()] ";"
}

//ExprOrAssign -> "(" [Args] ")" ";"
//              | [ "[" Expr() "]" ] "=" Expr() ";"
void ExprOrAssign():{}
{
    "(" [Args()] ")" ";"
  | [ "[" Expr() "]" ] "=" Expr() ";"
}

// Args -> Expr {"," Expr}
//
void Args(): {}
{
  Expr() ("," Expr())*
}

// PrArg -> Expr | <STRLIT>
//
void PrArg(): {}
{
  Expr() | <STRLIT>
}

// ExprNew -> BasicType "[" <INTLIT> "]"
//          | <ID> "(" ")"
void ExprNew(): {}
{
    BasicType() "[" <INTLIT> "]"
  | <ID> "(" ")"
}


// Expr -> AndRule {"||" AndRule}
void Expr():{}
{
    AndRule() ("||" AndRule())*
}

// AndRule -> CompRule {"&&" CompRule}
void AndRule():{}
{
    CompRule() ("&&" CompRule())*
}

//CompRule -> AddSubRule {Compare AddSubRule}
void CompRule():{}
{
    AddSubRule() (Compare() AddSubRule())*
}

//Compare -> "==" | "!=" | "<" | "<=" | ">" | ">="
void Compare():{} 
{
    "==" | "!=" | "<" | "<=" | ">"  | ">=" 
}

//AddSubRule -> MulDivRule {AddSub MulDivRule}
void AddSubRule():{}
{
    MulDivRule() (AddSub() MulDivRule())*
}

void AddSub():{}
{
    "+" | "-"
}

//MulDivRule -> UnOpRule {MulDiv UnOpRule}
void MulDivRule():{}
{
    UnOpRule() (MulDiv() UnOpRule())*
}

//MulDiv -> "*" | "/"
void MulDiv():{}
{
    "*" | "/"
}

//UnOpRule -> UnOp MethodCallRule | MethodCallRule
void UnOpRule():{}
{
     UnOp() UnOpRule() |  MethodCallRule()
}

//UnOp -> "-" | "!"
void UnOp():{}
{
    "-" | "!"
}

//MethodCallRule -> ParensNewRule | ExtId ExprOrMethod
void MethodCallRule():{}
{
      ParensNewRule()| ExtId() ExprOrMethod()
}

//ExprOrMethod -> "(" [Args] ")" | [ "[" Expr "]" ]
void ExprOrMethod():{}
{
    "(" [Args() ] ")"
  | ["[" Expr() "]" ]  
}

//ParensNewRule -> "(" Expr ")" | "new" ExprNew | Literal
void ParensNewRule():{}
{
    "(" Expr() ")" | "new" ExprNew() | Literal()
}

//ExtId -> ["this" "." ] <ID> {"."  <ID> }
void ExtId():{}
{
    ["this" "." ] <ID> ("." <ID> )*
}

//Literal -> <INTLIT> | DBLLIT | "true" | "false"
void Literal():{}
{
    <INTLIT> | <DBLLIT> | "true" | "false"
}
